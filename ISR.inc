;CBLOCK
;    BANDERA
;    TECLA
;    S_TEMP
;    W_TEMP
;ENDC
    
ISR:
    CALL    PUSH	    ;SALVADO DE CONTEXTO
    BTFSS   INTCON,RBIF	    ; SE DETERMINA QUE TIPO DE INTERRUPCION SE PRODUJO
	GOTO    FIN_ISR
    CLRF    FLAG_ISR	    ;LIMPIA LA BANDERA QUE CONTIENE INFORMACIÓN DE LA INTERRUPCIÓN ANTERIOR
    MOVLW   0XF0	    ;INTERRUPCIÓN POR DESOPRIMIR LA TECLA, SE DESECHA
    XORWF   PORTB,W
    BTFSC   STATUS,Z
	GOTO    FIN_ISR
    CALL    LEER_TECLADO    ;LECTURA DEL TECLADO
    MOVWF   TECLA	    ;LECTURA DIFERENTE A LA ESPERADA, SE DESECHADA
    MOVLW   0X80	    
    XORWF   TECLA,W
    BTFSC   STATUS,Z
	GOTO	FIN_ISR
    VAL_ADM:		;VALOR ADMITIDO
    BSF	    FLAG_ISR,0	    ;BANDERA DE VALOR ADMITIDO
    MOVLW   0X0A
    XORWF   TECLA,0
    BTFSS   STATUS,Z
	GOTO	EV_B	    ;GOTO   EVALUACIÓN DE B
    BSF	    FLAG_ISR,3	    ;SET BANDERA DE ENTER
    GOTO    FIN_ISR
    EV_B:
    MOVLW   0X0B
    XORWF   TECLA,0
    BTFSS   STATUS,Z
	GOTO	EV_C	    ;GOTO EVALUACIÓN DE C
    BSF	    FLAG_ISR,2	    ;SET BANDERA DE ATRAS
    GOTO    FIN_ISR
    EV_C:
    MOVLW   0X0C
    XORWF   TECLA,0
    BTFSS   STATUS,Z
	GOTO	EV_D	    ;GOTO	EVALUACIÓN DE D	    ;------------------------------------------
    MOVF    CONT_CONTRASENA,1	    ;EJECUCUÓN DEL BORRADO DE UN CARACTER
    BTFSC   STATUS,Z
	GOTO	FIN_ISR
    BSF	    FLAG_ISR,5		    ;ENCENDIDO DE LA BANDERA DE BORRADO UN CARACTER
    DECF    CONT_CONTRASENA,1
    MOVLW   0X22		    
    MOVWF   FSR	
    C_1:
    MOVF    INDF,0
    INCF    FSR,1
    MOVWF   INDF
    DECF    FSR,1
    DECF    FSR,1
    MOVLW   0X1F
    XORWF   FSR,0
    BTFSS   STATUS,Z 
	GOTO	C_1 
	
    INCF    FSR,1
    CLRF    INDF
    GOTO    FIN_ISR
    EV_D:		
    MOVLW   0X0D
    XORWF   TECLA,0
    BTFSS   STATUS,Z
	GOTO	EV_T	    ;GOTO EVALUACIÓN DE TECLAS
    BSF	    FLAG_ISR,4	    ;BANDERA DE GOTO INICIO
    GOTO    FIN_ISR
    EV_T:
    MOVLW   0X04
    XORWF   CONT_CONTRASENA,0
    BTFSC   STATUS,Z
	GOTO	FIN_ISR
    INCF    CONT_CONTRASENA,1
    BSF	    FLAG_ISR,1		;BANDERA DE ASIGNACIÓN DE UN CARACTER VALIDO
    ;ASIGNACIÓN DE LOS DATOS INGRESADOS EN EL VECTOR DE REGISTROS NUMERO
    MOVLW   0X21
    MOVWF   FSR	
    T_1:
    MOVF    INDF,0
    DECF    FSR,1
    MOVWF   INDF
    INCF    FSR,1
    INCF    FSR,1
    MOVLW   0X24
    XORWF   FSR,0
    BTFSS   STATUS,Z
	GOTO	T_1
    DECF    FSR,1
    MOVF    TECLA,0
    MOVWF   INDF
FIN_ISR:
    ;RETORNO DE LOS VALORES PREDETERMINADO PARA LA EJECUCIÓN NORMAL DE LA INTERRUPCIÓN
    MOVLW   0XF0
    MOVWF   PORTB
    MOVF    PORTB,W
    BCF     INTCON,0
    CALL    POP		;RETORNO DEL CONTEXTO
FIN_ISR_:
    RETFIE

; LECTURA Y DECODIFICACIÓN DEL TECLADO
LEER_TECLADO:
    MOVLW   0XF7
    MOVWF   PORTB
    MOVLW   0X80
    BTFSS   PORTB,7
    MOVLW   0X01
    BTFSS   PORTB,6
    MOVLW   0X04
    BTFSS   PORTB,5
    MOVLW   0X07
    BTFSS   PORTB,4
    MOVLW   0X0F
    BSF     PORTB,3
    BCF     PORTB,2
    BTFSS   PORTB,7
    MOVLW   0X02
    BTFSS   PORTB,6
    MOVLW   0X05
    BTFSS   PORTB,5
    MOVLW   0X08
    BTFSS   PORTB,4
    MOVLW   0X00
    BSF     PORTB,2
    BCF     PORTB,1
    BTFSS   PORTB,7
    MOVLW   0X03
    BTFSS   PORTB,6
    MOVLW   0X06
    BTFSS   PORTB,5
    MOVLW   0X09
    BTFSS   PORTB,4
    MOVLW   0X0E
    BSF     PORTB,1
    BCF     PORTB,0
    BTFSS   PORTB,7
    MOVLW   0X0A
    BTFSS   PORTB,6
    MOVLW   0X0B
    BTFSS   PORTB,5
    MOVLW   0X0C
    BTFSS   PORTB,4
    MOVLW   0X0D
    RETURN


; GUARDADO DE CONEXTO
PUSH:
    MOVWF   W_TEMP
    SWAPF   STATUS,W
    MOVWF   S_TEMP
    RETURN

;RECUPERA EL CONTEXTO
POP:
    SWAPF   S_TEMP,W
    MOVWF   STATUS
    SWAPF   W_TEMP,F
    SWAPF   W_TEMP,W
    RETURN



