;CBLOCK
;    LCD_BUSSY_FLAG
;ENDC

;DEFINICIONES DE LA LIBRERIA PARA MÁS FÁCIL ENTENDIMIENTO Y MODIFICACION
#define BUS_DATOS PORTD		    ;BUS DE DATOS
#define RS        PORTE,0           ;leer datos de la LCD
#define RW        PORTE,1           ;escribir datos
#define E         PORTE,2           ;enable

;CONFIGURACION DE LOS PUERTOS DEL MICROCONTROLADOR PARA EL USO DE LA LCD
LCD_CONFIG:
    BSF     STATUS,RP0
    BCF     STATUS,RP1
    CLRF    BUS_DATOS
    BCF     RS
    BCF     RW
    BCF     E
    BCF     STATUS,RP0
    BCF     STATUS,RP1
    CLRF    BUS_DATOS
    BCF     RS
    BCF     RW
    BCF     E
RETURN


; SECUENCIA DE INICIALIZACIÓN REQUERIDA POR LA LCD
LCD_INIT:
    CALL    RET40ms
    CALL    LCD_CONFIG
    MOVLW   0X30                ; se manda a W antes de irse a cada surtunica de escritura o de comando
    CALL    LCD_CMD
    CALL    RET10ms
    MOVLW   0X30
    CALL    LCD_CMD
    CALL    RET10ms
    MOVLW   0X30
    CALL    LCD_CMD
    CALL    RET1ms
    MOVLW   0X3F
    CALL    LCD_CMD
    MOVLW   0X0C
    CALL    LCD_CMD
    MOVLW   0X06
    CALL    LCD_CMD
    MOVLW   0X01
    CALL    LCD_CMD
RETURN

; EJECUCIÓN DE UN COMANDO CON LA LCD
LCD_CMD:
    MOVWF   BUS_DATOS
    BCF     RS
    BCF     RW    
    NOP
    BSF     E
    NOP
    NOP
    BCF     E
    CALL    ESPERAR_LCD
RETURN

;EJECUCIÓN DE LA ESCRITURA EN PANTALLA EN LA LCD
LCD_DATO:
    MOVWF   BUS_DATOS
    BSF     RS
    BCF     RW
    NOP
    BSF     E
    NOP
    NOP
    BCF     E    
    BCF     RS
    CALL    ESPERAR_LCD
RETURN

;ESTA SUBRUTINA ESPERA HASTA QUE LA BANDERA BF DE LA PANTALLA LCD SEA CERO,
;LO CUAL INDICA QUE LA LCD SE ENCIENTRA DISPONIBLE PARA RECIBIR OTRO DATO O COMANDO
ESPERAR_LCD:
    ; SE CONFIGURA EL PUERTO DE DATOS COMO ENTRADA
    BSF     STATUS,RP0
    MOVLW   0XFF
    MOVWF   BUS_DATOS
    BCF     STATUS,RP0
    ; RS=0 RW=1 MODO COMANDO Y SE PREPARA LA LCD PARA UNA LECTURA
    BCF     RS
    BSF     RW
    NOP             ;ESPERA UN TIEMPO ANTES DEL FLANCO ASCENDENTE DE ENABLE
    BSF     E       ; E=1
    NOP             ; ESPERA DE 500ns 1 Tcy PARA OBTENER EL VALOR CORRECTO DESDE LA LCD
    MOVF    BUS_DATOS,W         ; LECTURA DE DATOS DESDE LA LCD
    MOVWF   LCD_BUSSY_FLAG      ; ALMACENAMIETO EN UNA VARIABLE TEMPORAL 
    NOP                         ; ESPERA UN Tcy
    BCF     E                   ; E=0
    BTFSC   LCD_BUSSY_FLAG,7    ; SE VERIFICA EL BIT 7 BF DE LA LCD 
    GOTO    $-.10               ; SI ES 1 INDICA QUE ESTA OCUPADA, EN DICHO CASO RETROCEDEMOS 10 INSTRUCCIONES
    BSF     STATUS,RP0          ; SI BF ES CREO PODEMOS CONTINUAR, POR LO CUAL VOLVEMOS A PONER                               
    CLRF    BUS_DATOS            ;EL PUERTO DE DATOS COMO UNA SALIDA
    BCF     STATUS,RP0
    BCF     RW
RETURN


SHIFT_SCREEN_D:
    MOVLW   b'00011000'   ;DESPLAZA LA PANTALLA HACIA LA DERECHA
    CALL    LCD_CMD
RETURN

SHIFT_SCREEN_I:
    MOVLW   b'00011100'   ;DESPLAZA LA PANTALLA HACIA LA IZQUIERDA
    CALL    LCD_CMD
RETURN


CLRSCR:
    MOVLW   b'00000001' ;CLR LA PANTALA Y VA A HOME
    CALL    LCD_CMD
RETURN


ENTER:
    MOVLW   b'11000000'  ;POSICIONA EL CURSOR EN 0X40
    CALL    LCD_CMD
RETURN

CURSOR_D:
    MOVLW   b'00010100' ;MOVER EL CURSOR A LAS DERECHA
    CALL    LCD_CMD
RETURN

CURSOR_I:
    MOVLW   b'00010000'   ;MOVER EL CURSOR A LA IZQUIERDA
    CALL    LCD_CMD
RETURN
